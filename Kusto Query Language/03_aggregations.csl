//------------------------------------------------------------------------------
// 03_aggregations.csl
// Written by Taiob Ali
// SqlWorldWide.com
//------------------------------------------------------------------------------
// Go to: https://aka.ms/LADemo
// Will mostly use LogManagement/AzureMetrics table (loosely speaking)
//------------------------------------------------------------------------------

//------------------------------------------------------------------------------
// arg_max / arg_min
// https://docs.microsoft.com/en-us/azure/kusto/query/arg-max-aggfunction
// https://docs.microsoft.com/en-us/azure/kusto/query/arg-min-aggfunction
//------------------------------------------------------------------------------

// arg_max finds the maximum value for the column being summarized on, and 
// returns the row where that maximum value was found
// The second parameter indicates which columns to return. * means all.
// By default it always returns the "by" column and the maximized value.
//copied from pluralsight examples
Perf
| summarize arg_max(CounterValue, TimeGenerated, Computer) by CounterName
| sort by CounterName asc

//------------------------------------------------------------------------------
// Percentiles (https://docs.microsoft.com/en-us/azure/kusto/query/percentiles-aggfunction)
//------------------------------------------------------------------------------

// Percentiles calculates the value that is greater than x% of the sampleset. 
// You can rename the default column names with project-rename
Usage
| where TimeGenerated > ago(7d)
| summarize percentiles(Quantity, 50, 95) by DataType
| project-rename percent50 = percentile_Quantity_50 
                ,percent95 = percentile_Quantity_95 

//------------------------------------------------------------------------------
// countif (https://docs.microsoft.com/en-us/azure/kusto/query/countif-aggfunction)
//------------------------------------------------------------------------------

// Returns a count of rows for which Predicate evaluates to true
let T = datatable(name:string, day_of_birth:long)
[
   "John", 9,
   "Paul", 18,
   "George", 25,
   "Ringo", 7
];
T
| summarize countif(strlen(name) > 4)

//------------------------------------------------------------------------------
// top-nested (https://docs.microsoft.com/en-us/azure/kusto/query/topnestedoperator)
//------------------------------------------------------------------------------

// Produces hierarchical top results, where each level is a drill-down based on previous level values 
// You can add a row for all other records
AzureMetrics
| top-nested 3 of MetricName by MetricCount = count() 
   , top-nested 3 of ResourceGroup by ResourceGroupCount = count() 
|sort by MetricName asc 
       , ResourceGroup asc

//------------------------------------------------------------------------------
// any (https://docs.microsoft.com/en-us/azure/kusto/query/any-aggfunction)
//------------------------------------------------------------------------------

// Returns a random row for each distinct value
AzureMetrics
| summarize any(*) by MetricName
| sort by MetricName asc
